#pragma kernel CSMain

struct ShapeData
{
    float3 Pos;
    float Smoothness;
    float Radius;
};

RWTexture2D<float4> MapTex;
RWStructuredBuffer<ShapeData> SphereData;
RWStructuredBuffer<ShapeData> TorusData;
RWStructuredBuffer<ShapeData> CubeData;

uniform float2 Resolution;
uniform float3 SunDir;
uniform float3 SkyLight = float3(0, 1, 0);

uniform float RotAngle;
uniform float3 PlayerInput;
uniform float3 Gravity;

uniform float Time;
uniform float Delta;
uniform int MaxRaySteps;

uniform uint TorusCount;
uniform uint SphereCount;
uniform uint CubeCount;

uniform float BlendAmount;


//mixing smooth min and max functions originally created by inigo quilez.
float smoothblend(float a, float b, float K)
{
    //return min(a, b);
    float k = K * BlendAmount;
    float height = max(abs(k) - abs(a - b), 0.0) / k;
    if (k < 0.0)
    {
        //blend with shape.
        return min(a, b) - height * height/(abs(k) * 4.0);
    }

    //cut away from shape
    return max(-a, b) + height * height / (k * 4.0);
}

float sdfSphere(in float3 pos, in float radius)
{
    return length(pos) - radius;
}

float sdfCube(in float3 pos, in float radius)
{
    float3 q = abs(pos) - radius;
    return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0);
}

float sdfTorus(in float3 pos, in float radius)
{
    float thickness = radius / 2.0;
    float2 q = float2(length(pos.xz) - radius, pos.y);
    return length(q) - thickness;
}

float sdfElipsoid(in float3 pos, in float3 radius)
{
    float ka = length(pos / radius);
    float kb = length(pos / radius / radius);

    return ka *(ka - 1.0) /kb;
}

float Ground(in float3 pos, in float3 normal, in float distance)
{
    return dot(pos, normal) + distance;
}

float map(in float3 pos)
{
    float T = Ground(pos, float3(0, 1, 0), 1.0);
    //float T = sdfSphere(pos + SphereData[0].Pos, SphereData[0].Radius);
    for (uint i = 0; i < SphereCount; i++)
    {
        float ta = sdfSphere(pos + SphereData[i].Pos, SphereData[i].Radius);
        if (ta < T)
        {
            T = smoothblend(ta, T, SphereData[i].Smoothness);
        }
    }

    for (i = 0; i < TorusCount; i++)
    {
        float ta = sdfTorus(pos + TorusData[i].Pos, TorusData[i].Radius);
        if (ta < T)
        {
            T = smoothblend(ta, T, TorusData[i].Smoothness);
        }
    }

    for (i = 0; i < CubeCount; i++)
    {
        float ta = sdfCube(pos + CubeData[i].Pos, CubeData[i].Radius);
        if (ta < T)
        {
            T = smoothblend(ta, T, CubeData[i].Smoothness);
        }
    }
    return T;
}
float3 CalcNormal(in float3 pos)
{
    float2 e = float2(0.00001, 0.0);//accuracy of a normal
    return normalize(float3(map(pos + e.xyy) - map(pos - e.xyy),
        map(pos + e.yxy) - map(pos - e.yxy),
        map(pos + e.yyx) - map(pos - e.yyx)));
}
float TraceRay(float3 RO, float3 RD)
{
    float t = 0.0;
    for (int i = 0; i < MaxRaySteps; i++)
    {
        float3 pos = RO + t * RD;
        float h = map(pos);
        if (h < 0.0001)
        {
            break;
        }
        t += h;
        if (t > 2000.0)
        {
            t = -1.0;
            break;
        }
    }
    return t;
}

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    //P translates the resolution coordinates into -1 to 1 screen space.
    float2 p = (2.0 * (float2)id.xy - Resolution.xy) / Resolution.y;

    float3 RayOrigin = float3(PlayerInput.xy, PlayerInput.z);
    float3 Rot = RayOrigin + float3(1.3 * cos(RotAngle), -0.25, 1.3 * sin(RotAngle)); //y is angle 

    //cam rot matrix
    float3 cw = normalize(RayOrigin - Rot);//direction
    float3 cp = float3(0, 1, 0); //up
    float3 cu = normalize(cross(cw, cp));//get right direction
    float3 cv = cross(cu, cw);//up normalised.

    //float3 RayDir = normalize(float3(p, -Resolution.x/Resolution.y));
    float3 RayDir = normalize(float3(p.x * cu + p.y * cv + -1.9 * cw));

    float3 Colour = float3(0.6, 0.67, 0.999) - 0.5 * RayDir.y;

    //Raymarch
    float t = TraceRay(RayOrigin, RayDir);

    //Ray hit something
    if (t > 0.0)
    {
        float3 pos = RayOrigin + t * RayDir;
        float3 normal = CalcNormal(pos);
        float SunDiffuse = clamp(dot(normal, SunDir), 0.0, 1.0);
        float SunShadow = step(TraceRay(pos + normal * 0.01, SunDir), 0.0);
        float SkyDiffuse = clamp(dot(normal, SkyLight), 0.0, 1.0);
        float SunBounce = clamp(dot(normal, -SkyLight), 0.0, 1.0);
        Colour = float3(1.0, 0.90, 0.80) * SunDiffuse * SunShadow;
        Colour += float3(0.0, 0.2, 0.4) * SkyDiffuse;
        Colour += float3(0.0, 0.20, 0.30) * SunBounce;
    }

    Colour = pow(Colour, float3(0.4545, 0.4545, 0.4545)); //correct lighting for eyes.

    MapTex[id.xy] = float4(Colour, 1.0);
}